extends Control

signal host_requested(p_player_name, p_port, p_server_only)

@export_file
var const_error_dialog_scene : String = "res://vsk_speech/scenes/error_dialog.tscn"

var error_dialog : Node = load(const_error_dialog_scene).instantiate()

func _on_host_pressed() -> void:
	if get_node("connect/name").text == "":
		get_node("connect/error_label").text = "Invalid name!"
		return

	get_node("connect").hide()
	get_node("players").show()
	get_node("connect/error_label").text = ""

	var player_name : String = get_node("connect/name").text
	var port : int = get_node("connect/port").value

	emit_signal("host_requested", player_name, port, true)

func _on_join_pressed() -> void:
	if get_node("connect/name").text == "":
		get_node("connect/error_label").text = "Invalid name!"
		return

	var port : int = get_node("connect/port").value

	var ip : String = get_node("connect/ip").text
	if not ip.is_valid_ip_address():
		get_node("connect/error_label").text = "Invalid IPv4 address!"
		return

	get_node("connect/error_label").text=""
	get_node("connect/host").disabled = true
	get_node("connect/join").disabled = true

	var player_name : String = get_node("connect/name").text
	network_layer.join_game(ip, port, player_name)

func on_connection_success() -> void:
	get_node("connect").hide()
	get_node("players").show()
	# on_game_error("Connected")

func on_connection_failed() -> void:
	get_node("connect/host").disabled = false
	get_node("connect/join").disabled = false
	get_node("connect/error_label").set_text("Connection failed.")

func on_game_ended() -> void:
	show()
	get_node("connect").show()
	get_node("players").hide()
	get_node("connect/host").disabled = false

func on_game_error(p_errtxt : String) -> void:
	if error_dialog.get_parent() == null:
		error_dialog.set_name("error")
		add_child(error_dialog, true)
	get_node("error").dialog_text = p_errtxt
	get_node("error").popup_centered()

func refresh_lobby(p_player_names : Array) -> void:
	get_node("players/list").clear()

	for p in p_player_names:
		get_node("players/list").add_item(p)


func _on_playback_mute_pressed(toggled_on: bool) -> void:
	AudioServer.set_bus_mute(AudioServer.get_bus_index("Master"), toggled_on)


func _on_mic_mute_pressed(toggled_on: bool) -> void:
	AudioServer.set_bus_mute(AudioServer.get_bus_index("Mic"), toggled_on)
	var mic_bus_idx = AudioServer.get_bus_index("Mic")
	for i in range(AudioServer.get_bus_effect_count(mic_bus_idx)):
		AudioServer.set_bus_effect_enabled(mic_bus_idx, i, not toggled_on)


func _on_input_device_selected(idx: int) -> void:
	var device_name: String = get_node("connect/input_device").get_item_text(idx)
	AudioServer.input_device = device_name
	print("Input device: %s" % device_name)


func _on_output_device_selected(idx: int) -> void:
	var device_name: String = get_node("connect/output_device").get_item_text(idx)
	AudioServer.output_device = device_name
	print("Output device: %s" % device_name)


func _populate_devices() -> void:
	var input_btn: OptionButton = get_node("connect/input_device")
	var output_btn: OptionButton = get_node("connect/output_device")
	input_btn.clear()
	output_btn.clear()

	for device in AudioServer.get_input_device_list():
		input_btn.add_item(device)
	if input_btn.item_count > 0:
		var current := AudioServer.input_device
		for i in range(input_btn.item_count):
			if input_btn.get_item_text(i) == current:
				input_btn.select(i)
				break

	for device in AudioServer.get_output_device_list():
		output_btn.add_item(device)
	if output_btn.item_count > 0:
		var current := AudioServer.output_device
		for i in range(output_btn.item_count):
			if output_btn.get_item_text(i) == current:
				output_btn.select(i)
				break


func _ready() -> void:
	_populate_devices()
